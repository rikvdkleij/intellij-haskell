// Originally derived from http://www.haskell.org/onlinereport/haskell2010/haskellch10.html
{
  parserClass="intellij.haskell.parser.HaskellParser"
  parserUtilClass="intellij.haskell.psi.HaskellParserUtil"

  implements="intellij.haskell.psi.HaskellCompositeElement"
  extends="intellij.haskell.psi.impl.HaskellCompositeElementImpl"

  psiClassPrefix="Haskell"
  psiImplClassSuffix="Impl"
  psiPackage="intellij.haskell.psi"
  psiImplPackage="intellij.haskell.psi.impl"

  elementTypeHolderClass="intellij.haskell.psi.HaskellTypes"
  elementTypeClass="intellij.haskell.psi.HaskellCompositeElementType"
  tokenTypeClass="intellij.haskell.psi.HaskellTokenType"

  psiImplUtilClass="intellij.haskell.psi.impl.HaskellPsiImplUtil"

  elementTypePrefix="HS_"

  methods("varid|varsym|conid|consym|modid")=[getName setName getNameIdentifier getReference getPresentation]
  extends("varid|varsym|conid|consym|modid")="intellij.haskell.psi.impl.HaskellNamedStubBasedPsiElementBase<?>"
  implements("varid|varsym|conid|consym|modid")="intellij.haskell.psi.HaskellNamedElement"
  elementTypeFactory("varid|varsym|conid|consym|modid")="intellij.haskell.psi.impl.HaskellElementTypeFactory.factory"

  methods("type_signature|type_declaration|class_declaration|instance_declaration|type_family_declaration|deriving_declaration|type_instance_declaration")=[getName getPresentation getIdentifierElements getModuleName]
  methods("default_declaration|foreign_declaration|module_declaration")=[getName getPresentation getIdentifierElements getModuleName]
  implements("type_signature|type_declaration|class_declaration|instance_declaration|type_family_declaration|deriving_declaration|type_instance_declaration|default_declaration")="intellij.haskell.psi.HaskellDeclarationElement"
  implements("default_declaration|foreign_declaration|module_declaration")="intellij.haskell.psi.HaskellDeclarationElement"

  methods("data_declaration|newtype_declaration")=[getName getPresentation getIdentifierElements getModuleName getDataTypeConstructor]
  implements("data_declaration|newtype_declaration")="intellij.haskell.psi.HaskellDataConstructorDeclarationElement"

  methods("var_con|q_var_con")=[getName getIdentifierElement]

  methods("q_name|cname")=[getName getIdentifierElement getQualifierName getNameWithoutParens]
  implements("q_name|cname")="intellij.haskell.psi.HaskellQualifiedNameElement"

  implements("line_expression|last_line_expression")="intellij.haskell.psi.HaskellLineExpressionElement"
  mixin("|line_expression|last_line_expression")="intellij.haskell.psi.impl.HaskellLineExpressionElementImpl"

  methods("var|con|varop|conop")=[getName]
  implements("var|con|varop|conop")="intellij.haskell.psi.HaskellCNameElement"
  mixin("var|con|varop|conop")="intellij.haskell.psi.impl.HaskellCNameElementImpl"

  methods("q_con_qualifier1|q_con_qualifier2|q_con_qualifier3|q_con_qualifier4|qualifier")=[getName setName getNameIdentifier getReference getPresentation getUseScope]
  implements("q_con_qualifier1|q_con_qualifier2|q_con_qualifier3|q_con_qualifier4|qualifier")="intellij.haskell.psi.HaskellQualifierElement"
  mixin("q_con_qualifier1|q_con_qualifier2|q_con_qualifier3|q_con_qualifier4|qualifier")="intellij.haskell.psi.impl.HaskellQualifierElementImpl"
}

program                     ::= onl SHEBANG_LINE? onl file_header? onl module_body

file_header                 ::= (file_header_pragma onl)+
file_header_pragma          ::= language_pragma | options_ghc_pragma | include_pragma | haddock_pragma | ann_pragma | DIRECTIVE | dummy_pragma
language_pragma             ::= PRAGMA_START onl "LANGUAGE" onl q_name (onl COMMA onl q_name?)* onl PRAGMA_END NEWLINE?
options_ghc_pragma          ::= PRAGMA_START onl ("OPTIONS_GHC" | "OPTIONS") onl options_ghc_option+ onl PRAGMA_END NEWLINE?
options_ghc_option          ::= (VAR_ID ("-" (VAR_ID | TYPE))* | ("-" (VAR_ID | TYPE))+ | "-" CON_ID) (EQUAL literal)?
include_pragma              ::= PRAGMA_START onl "INCLUDE" general_pragma_content PRAGMA_END NEWLINE?
haddock_pragma              ::= PRAGMA_START onl "OPTIONS_HADDOCK" onl (VARSYM_ID | VAR_ID)+ onl (COMMA onl (VARSYM_ID | VAR_ID)+)* onl PRAGMA_END NEWLINE?
dummy_pragma                ::= PRAGMA_START q_name? PRAGMA_END? NEWLINE?

other_pragma                ::= ann_pragma | deprecated_warn_pragma | noinline_pragma | inlinable_pragma | line_pragma | rules_pragma |
                                  specialize_pragma | inline_pragma | minimal_pragma | overlap_pragma | constant_folded_pragma | dummy_pragma
deprecated_warn_pragma      ::= PRAGMA_START onl ("DEPRECATED" | "WARNING") general_pragma_content PRAGMA_END
inline_pragma               ::= PRAGMA_START onl "INLINE" general_pragma_content PRAGMA_END
noinline_pragma             ::= PRAGMA_START onl ("NOINLINE" | "NOTINLINE") general_id+ PRAGMA_END
specialize_pragma           ::= PRAGMA_START onl ("SPECIALIZE" | "SPECIALISE") general_pragma_content PRAGMA_END

inlinable_pragma            ::= PRAGMA_START onl ("INLINABLE" | "INLINEABLE") general_pragma_content PRAGMA_END
ann_pragma                  ::= PRAGMA_START onl "ANN" general_pragma_content PRAGMA_END
line_pragma                 ::= PRAGMA_START onl "LINE" general_pragma_content PRAGMA_END
rules_pragma                ::= PRAGMA_START onl "RULES" general_pragma_content PRAGMA_END

source_pragma               ::= PRAGMA_START onl "SOURCE" onl PRAGMA_END
minimal_pragma              ::= PRAGMA_START onl "MINIMAL" onl general_pragma_content onl PRAGMA_END
unpack_pragma               ::= PRAGMA_START onl "UNPACK" onl PRAGMA_END
nounpack_pragma             ::= PRAGMA_START onl "NOUNPACK" onl PRAGMA_END
ctype_pragma                ::= PRAGMA_START onl "CTYPE" onl general_pragma_content onl PRAGMA_END
overlap_pragma              ::= PRAGMA_START onl ("OVERLAPPABLE" | "OVERLAPPING" | "OVERLAPS") onl PRAGMA_END
constant_folded_pragma      ::= PRAGMA_START onl "CONSTANT_FOLDED" onl general_pragma_content onl PRAGMA_END
unpack_nounpack_pragma      ::= unpack_pragma | nounpack_pragma | ctype_pragma
scc_pragma                  ::= PRAGMA_START onl "SCC" onl general_pragma_content onl PRAGMA_END
incoherent_pragma           ::= PRAGMA_START onl "INCOHERENT" onl PRAGMA_END
general_pragma_content      ::= (CON_ID | VAR_ID | CONSYM_ID | VARSYM_ID | DOT |
                                  LEFT_PAREN | RIGHT_PAREN | FLOAT | DO | WHERE | IF | THEN | ELSE |
                                  COLON_COLON | DOUBLE_RIGHT_ARROW | RIGHT_ARROW | IN | CASE | OF | LET |
                                  SEMICOLON | LEFT_ARROW | LEFT_BRACKET | RIGHT_BRACKET | literal | LEFT_BRACE | RIGHT_BRACE |
                                  COMMA | UNDERSCORE | symbol_reserved_op | QUOTE | BACKQUOTE | DOT_DOT | MODULE | INSTANCE | NEWLINE)+

cfiles_pragma               ::= PRAGMA_START onl "CFILES" general_pragma_content PRAGMA_END

module_body                 ::= module_declaration onl body | onl body
module_declaration          ::= MODULE modid onl deprecated_warn_pragma? onl (exports onl)? WHERE

private body                ::= import_declarations top_declarations (NEWLINE | DIRECTIVE)*
import_declarations         ::= ((import_declaration | cfiles_pragma | DIRECTIVE) onl)*
private top_declarations    ::= (top_declaration (NEWLINE | DIRECTIVE)+)* top_declaration?

import_declaration          ::= IMPORT onls source_pragma? import_package_name? onls import_qualified? onls modid onls import_qualified_as? onls import_spec? NEWLINE?   {methods=[getModuleName] pin=1}
import_qualified            ::= VAR_ID      // "qualified"
import_qualified_as         ::= VAR_ID qualifier    // "as" qualifier
import_spec                 ::= import_ids_spec |
                                  import_hiding_spec |
                                  import_empty_spec

import_ids_spec             ::= LEFT_PAREN onls import_id (onls COMMA? onls import_id)* onls (COMMA)? onls RIGHT_PAREN
import_hiding_spec          ::= import_hiding onls LEFT_PAREN onls (import_id onls (onls COMMA onls import_id)* onls (COMMA)?)? onls RIGHT_PAREN
import_empty_spec           ::= LEFT_PAREN RIGHT_PAREN
import_hiding               ::= VAR_ID  //  "hiding"
import_id                   ::= TYPE? (cname onls dot_dot_parens | cname (LEFT_PAREN onls (cname onls (COMMA onls cname onls)* onls)? RIGHT_PAREN) | cname)
dot_dot_parens              ::= LEFT_PAREN DOT_DOT RIGHT_PAREN
import_package_name         ::= STRING_LITERAL

top_declaration             ::= type_declaration | data_declaration | newtype_declaration | class_declaration | instance_declaration | default_declaration |
                                  foreign_declaration | type_family_declaration | deriving_declaration | type_instance_declaration | type_signature |
                                  other_pragma | expression | cfiles_pragma | fixity_declaration | DIRECTIVE

type_declaration            ::= TYPE onls simpletype onls COLON_COLON onls ttype |
                                  TYPE onls simpletype onls (EQUAL | WHERE) onls (ttype | type_signature) (DOUBLE_RIGHT_ARROW ttype)? |
                                  TYPE onls simpletype onls EQUAL onls expression |
                                  TYPE onls simpletype onls (LEFT_PAREN onls kind_signature onls RIGHT_PAREN)* onls EQUAL onls ttype |
                                  TYPE onls simpletype


data_declaration            ::= DATA (onls ctype_pragma)? onls INSTANCE? onls (ccontext onls DOUBLE_RIGHT_ARROW)? onls simpletype onls (LEFT_PAREN onls kind_signature onls RIGHT_PAREN)* onls q_name* onls (EQUAL | WHERE)?
                                    onls (scontext onls DOUBLE_RIGHT_ARROW)? onls (constrs | simpletype | cidecls)? (onls data_declaration_deriving)? |
                                  DATA (onls ctype_pragma)? onls (LEFT_PAREN onls kind_signature onls RIGHT_PAREN)* onls simpletype onls (LEFT_PAREN onls kind_signature onls RIGHT_PAREN)* onls q_name* (onls data_declaration_deriving)?

kind_signature              ::= q_name COLON_COLON ttype

data_declaration_deriving   ::= (DERIVING onls ttype | DERIVING onls LEFT_PAREN ttype (onls COMMA onls ttype)* RIGHT_PAREN)+

newtype_declaration         ::= NEWTYPE onls INSTANCE? onls ctype_pragma? onls (ccontext onls DOUBLE_RIGHT_ARROW)? onls simpletype onls EQUAL onls newconstr onls (DERIVING onls ttype)?

class_declaration           ::= CLASS onls (scontext onls DOUBLE_RIGHT_ARROW)? onls (q_name+ | ttype) onls (q_name | LEFT_PAREN ttype (onls COMMA onls ttype)* RIGHT_PAREN)*
                                    (onls VERTICAL_BAR onls ttype (onls COMMA onls ttype)*)? onls WHERE? onls cidecls? |
                                  CLASS onls scontext onls DOUBLE_RIGHT_ARROW onls (q_name+ | ttype) onls WHERE? onls cidecls?  // seen in Control.Lens.Internal.Indexed

instance_declaration        ::= INSTANCE onls (overlap_pragma | "OVERLAPPABLE_" | "OVERLAPPING_" | incoherent_pragma)? onls (var_con+ DOT)? onls (scontext onls DOUBLE_RIGHT_ARROW)? onls q_name onls inst onls (WHERE onls cidecls)?

default_declaration         ::= DEFAULT onls (LEFT_PAREN (ttype (COMMA ttype)*)? RIGHT_PAREN | type_signature)

type_instance_declaration   ::= TYPE_INSTANCE onls expression

type_signature              ::= q_names onls COLON_COLON onls (ccontext DOUBLE_RIGHT_ARROW)* onls ttype |
                                  q_names onls LEFT_PAREN onls q_names onls COMMA onls ccontext onls DOUBLE_RIGHT_ARROW onls ttype onls RIGHT_PAREN   // seen in  Test.Hspec.Expectations: with_loc(shouldBe, (Show a, Eq a) => a -> a -> Expectation)

fixity_declaration          ::= fixity (DECIMAL)? q_names

foreign_declaration         ::= (FOREIGN_IMPORT | FOREIGN_EXPORT) onls expression

type_family_declaration     ::= TYPE_FAMILY onls type_family_type onls WHERE? onls expression?

deriving_declaration        ::= DERIVING INSTANCE (scontext onls DOUBLE_RIGHT_ARROW)? onls q_name onls inst


private literal             ::= DECIMAL | HEXADECIMAL | OCTAL | FLOAT | CHARACTER_LITERAL | STRING_LITERAL

private onl                 ::= (DIRECTIVE? NEWLINE)*
private onls                ::= (&<<containsSpaces>> NEWLINE | DIRECTIVE NEWLINE)*    // optional multiple (new line and spaces) or directives
private nls                 ::= (&<<containsSpaces>> NEWLINE)+                        // multiple new lines and spaces

comments                    ::= COMMENT | NCOMMENT | NCOMMENT_START | NCOMMENT_END  // rule is necessary to refer to various comment types in code

modid                       ::= (CON_ID DOT)* CON_ID {pin=1 recoverWhile=modid_recover_rule stubClass = "intellij.haskell.psi.stubs.HaskellModidStub"}
private modid_recover_rule  ::= DOT

varid                       ::= VAR_ID {stubClass = "intellij.haskell.psi.stubs.HaskellVaridStub"}
conid                       ::= CON_ID {stubClass = "intellij.haskell.psi.stubs.HaskellConidStub"}
varsym                      ::= VARSYM_ID | TILDE | (DOT | DOT_DOT) VARSYM_ID | (DOT | DOT_DOT) CONSYM_ID | (DOT | DOT_DOT) EQUAL | DOT {stubClass = "intellij.haskell.psi.stubs.HaskellVarsymStub"}
consym                      ::= CONSYM_ID   {stubClass = "intellij.haskell.psi.stubs.HaskellConsymStub"}

cname                       ::= var | con | conop | varop
var                         ::= varid | LEFT_PAREN varsym RIGHT_PAREN
con                         ::= conid | LEFT_PAREN consym RIGHT_PAREN
varop                       ::= varsym | BACKQUOTE varid BACKQUOTE
conop                       ::= consym | BACKQUOTE conid BACKQUOTE

var_con                     ::= varid | consym | varsym | conid
q_var_con                   ::= qualifier DOT (varid | consym | DOT? varsym) | q_con

qualifier                   ::= CON_ID (DOT CON_ID)*

q_con                       ::= q_con_qualifier4 DOT conid | q_con_qualifier3 DOT conid | q_con_qualifier2 DOT conid | q_con_qualifier1 DOT conid
q_con_qualifier4            ::= CON_ID DOT CON_ID DOT CON_ID DOT CON_ID
q_con_qualifier3            ::= CON_ID DOT CON_ID DOT CON_ID
q_con_qualifier2            ::= CON_ID DOT CON_ID
q_con_qualifier1            ::= CON_ID
q_con_qualifier             ::= q_con_qualifier4 | q_con_qualifier3 | q_con_qualifier2 | q_con_qualifier1  // used for renaming

q_name                      ::= q_var_con | var_con | LEFT_PAREN q_var_con RIGHT_PAREN | LEFT_PAREN var_con RIGHT_PAREN | BACKQUOTE q_var_con BACKQUOTE | BACKQUOTE var_con BACKQUOTE
q_names                     ::= q_name (COMMA q_name)*

gtycon                      ::= QUOTE? q_name | LEFT_PAREN RIGHT_ARROW RIGHT_PAREN | LEFT_PAREN RIGHT_PAREN | QUOTE? LEFT_BRACKET RIGHT_BRACKET | LEFT_PAREN COMMA (COMMA)* RIGHT_PAREN

private constrs             ::= (("forall" | "âˆ€") q_name DOT)? constr (onls VERTICAL_BAR onls constr)*
private constr              ::= (type_signature | constr1 | constr2 | constr3 | constr4)
constr1                     ::= unpack_nounpack_pragma? q_name onls unpack_nounpack_pragma? onls LEFT_BRACE onls (fielddecl ((onls COMMA)? onls fielddecl)*)? onls RIGHT_BRACE
constr2                     ::= unpack_nounpack_pragma? (q_name | LEFT_PAREN q_name* RIGHT_PAREN | LEFT_BRACKET q_name* RIGHT_BRACKET) onls (onls unpack_nounpack_pragma? onls ttype)*  // Base.hs: data () = () and GHC.Types.hs: data [] a = [] | a : [a]
constr3                     ::= unpack_nounpack_pragma? sub_constr2 onls unpack_nounpack_pragma? q_name onls unpack_nounpack_pragma? onls sub_constr2
constr4                     ::= unpack_nounpack_pragma? q_name+ unpack_nounpack_pragma? q_name
sub_constr2                 ::= (btype | q_name atype)

fielddecl                   ::= q_names (COLON_COLON unpack_nounpack_pragma? (ttype | q_name atype))?

private atype               ::= "forall" (q_name | ttype | LEFT_PAREN type_signature RIGHT_PAREN)+ DOT |
                                  onls LEFT_PAREN onls ttype+ onls RIGHT_PAREN |
                                  onls LEFT_PAREN onls ttype+ DOUBLE_RIGHT_ARROW onls ttype+ onls RIGHT_PAREN |
                                  onls LEFT_PAREN (onls VARSYM_ID)? onls ttype (onls COMMA onls ttype)* onls (VARSYM_ID onls)? RIGHT_PAREN |  // VARSYM_ID? is optional #
                                  QUOTE? LEFT_BRACKET onls ttype onls RIGHT_BRACKET |
                                  QUOTE? q_name+ | type_signature | QUOTE? LEFT_PAREN RIGHT_PAREN | QUOTE? LEFT_BRACKET RIGHT_BRACKET | LEFT_PAREN COMMA+ RIGHT_PAREN | DIRECTIVE

ttype                       ::= varsym | varsym? btype (onls RIGHT_ARROW onls ttype)* | list_type q_name* | LEFT_PAREN RIGHT_ARROW RIGHT_PAREN

private btype               ::= atype+     // generalization because left recursive is not supported

simpletype                  ::= ttype | (q_name | LEFT_PAREN RIGHT_PAREN)+ |
                                  q_name* onls LEFT_PAREN q_name RIGHT_PAREN onls q_name* |
                                  q_name onls q_name* onls (LEFT_PAREN type_signature RIGHT_PAREN)+ onls q_name* |
                                  q_name | LEFT_PAREN RIGHT_PAREN | LEFT_BRACKET RIGHT_BRACKET q_name*    {methods=[getIdentifierElements]}

list_type                   ::= LEFT_BRACKET (COLON_COLON | q_name) RIGHT_BRACKET

type_family_type            ::= (q_name | LEFT_PAREN? q_names COLON_COLON? onls (ccontext DOUBLE_RIGHT_ARROW)? onls ttype? RIGHT_PAREN?)+ (COLON_COLON onls ttype)?

ccontext                     ::= LEFT_PAREN onls clazz (onls COMMA onls clazz)* onls RIGHT_PAREN |
                                  clazz

clazz                       ::=  q_name COLON_COLON q_name | ttype |
                                  q_name+ |
                                  q_name LEFT_PAREN q_name atype+ RIGHT_PAREN |
                                  q_name LEFT_PAREN q_name+ RIGHT_PAREN q_name*

scontext                    ::= simpleclass | simpleclass onls LEFT_PAREN onls simpleclass (onls COMMA onls simpleclass)* onls RIGHT_PAREN

simpleclass                 ::= ttype | q_name+ LEFT_PAREN q_name+ RIGHT_PAREN q_name* | q_name+

inst                        ::= gtycon+ instvar* (LEFT_PAREN onls gtycon (instvar)* onls RIGHT_PAREN)+ instvar* |
                                  (LEFT_PAREN onls instvar (onls COMMA onls instvar)+ onls RIGHT_PAREN)+ instvar* |
                                  QUOTE? (LEFT_BRACKET onls instvar onls RIGHT_BRACKET)+ instvar* |
                                  (LEFT_PAREN onls instvar+ onls RIGHT_PAREN)+ instvar* |
                                  (LEFT_PAREN onls instvar+ (RIGHT_ARROW onls instvar* onls)* onls RIGHT_PAREN)+ instvar* |
                                  ttype

instvar                     ::= q_name | LEFT_BRACKET q_name+ RIGHT_BRACKET | LEFT_PAREN q_name+ RIGHT_PAREN | LEFT_PAREN RIGHT_PAREN

private fixity              ::= INFIXL | INFIXR | INFIX

exports                     ::= LEFT_PAREN onl export (onl COMMA? onl export onl)* onl COMMA? onl RIGHT_PAREN | LEFT_PAREN RIGHT_PAREN
export                      ::= export3 | export1 | export2 | export4
private export3             ::= (q_con | cname) onl LEFT_PAREN onl (cname onl (onl COMMA onl cname)*)? onl RIGHT_PAREN
private export1             ::= (q_con | cname) dot_dot_parens
private export2             ::= TYPE? onl cname+
private export4             ::= MODULE modid

newconstr                   ::= q_name atype | newconstr_fielddecl
newconstr_fielddecl         ::= q_name onls LEFT_BRACE? onls q_name onls COLON_COLON onls ttype onls RIGHT_BRACE?

cidecls                     ::= cidecl (onls cidecl)*

private cidecl              ::= type_signature | inline_pragma | noinline_pragma | specialize_pragma | type_declaration | instance_declaration | default_declaration |
                                  newtype_declaration | data_declaration | minimal_pragma | expression

expression                  ::= line_expression+ last_line_expression | last_line_expression
private line_expression     ::= general_id+ nls
private last_line_expression::= general_id+

private general_id          ::= q_name | LEFT_PAREN | RIGHT_PAREN | FLOAT |
                                  COLON_COLON | DOUBLE_RIGHT_ARROW | RIGHT_ARROW |
                                  SEMICOLON | LEFT_ARROW | LEFT_BRACKET | RIGHT_BRACKET | literal | LEFT_BRACE | RIGHT_BRACE |
                                  COMMA | symbol_reserved_op | QUOTE | BACKQUOTE | fixity | DOT_DOT | scc_pragma | reserved_id | DIRECTIVE

private symbol_reserved_op  ::= AT | BACKSLASH | VERTICAL_BAR | TILDE | EQUAL

reserved_id                 ::= CASE | CLASS | DATA | DEFAULT | DERIVING | DO | ELSE | IF | IMPORT | IN | INFIX | INFIXL | INFIXR | INSTANCE | LET | MODULE | NEWTYPE | OF | THEN | TYPE | WHERE | UNDERSCORE

